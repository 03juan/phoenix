defmodule Phoenix.Router.Route do
  # This module defines the Route struct that is used
  # throughout Phoenix's router.
  @moduledoc false

  alias Phoenix.Router.Route

  @doc """
  The Route struct. It stores:

  * :verb - the HTTP verb as an upcase string
  * :path - the route path as a normalized string
  * :segments - the route path as quoted segments
  * :params - the parameter names as a list of atoms
  * :controller - ?
  * :action - ?
  * :helper - the named of the named helper (may be nil)
  """
  defstruct [:verb, :path, :segments, :params, :controller, :action, :helper]
  @type t :: %Route{}

  @doc """
  Receives the verb, path, controller, action and helper
  and returns a Route struct. The given path is processed
  and validated, raising an error in case of invalid paths.
  """
  # TODO: Ensure path is normalized.
  # TODO: Test this.
  @spec build(String.t, String.t, atom, atom, atom) :: t
  def build(verb, path, controller, action, helper) when is_binary(verb) and is_binary(path) do
    {params, segments} = Plug.Router.Utils.build_match(path)
    %Route{verb: verb, path: path, segments: segments, params: params,
           controller: controller, action: action, helper: helper}
  end

  @doc """
  Receives a route and returns the quoted definition for the
  helper function in case a helper name was given, simply returns
  nil otherwise.
  """
  @spec helper_definition(t) :: Macro.t | nil
  def helper_definition(%Route{helper: nil}), do: nil

  def helper_definition(%Route{} = route) do
    helper = route.helper
    action = route.action
    params = route.params

    vars = Enum.map(params, &Macro.var(&1, nil))
    bins = Enum.map(params, &Atom.to_string/1)

    quote do
      def unquote(:"#{helper}_path")(unquote(action), unquote_splicing(vars)) do
        unquote(:"#{helper}_path")(unquote(action), unquote_splicing(vars), [])
      end

      def unquote(:"#{helper}_path")(unquote(action), unquote_splicing(vars), params) do
        Route.segments_to_path(unquote(route.segments), params, unquote(bins))
      end
    end
  end

  @doc """
  Receives a list of segments and params for query string and
  returns a path as a string.

  This function is invoked by the definitions generated by the
  `helper_definition/1` function.
  """
  def segments_to_path(segments, query, reserved) do
    "/" <> Enum.join(segments, "/") <> query_to_path(query, reserved)
  end

  defp query_to_path([], _reserved), do: ""
  defp query_to_path(query, reserved) do
    case Plug.Conn.Query.encode Dict.drop(query, reserved) do
      "" -> ""
      o  -> "?" <> o
    end
  end
end
